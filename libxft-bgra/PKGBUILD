_pkgbasever=2.3.3
pkgname=libxft-bgra
pkgver=2.3.3.r7.7808631e
pkgrel=1
pkgdesc="libXft with BGRA glyph (color emoji) rendering & scaling patches by Maxime Coste"
arch=('any')
license=('custom')
groups=('modified')
provides=('libxft')
conflicts=('libxft')
url="https://xorg.freedesktop.org/"
depends=('fontconfig' 'libxrender')
makedepends=('git' 'pkgconfig')
source=(${url}/releases/individual/lib/libXft-${_pkgbasever}.tar.bz2)
sha512sums=('SKIP')

LIBXFT_UPSTREAM_URL="https://gitlab.freedesktop.org/xorg/lib/libxft.git"
GITLAB_REVISION=7
COMMIT_ID=7808631e7a9a605d5fe7a1077129c658d9ec47fc

pkgver() {
  echo "${_pkgbasever}.r${GITLAB_REVISION}.`echo $COMMIT_ID | cut -c1-8`"
}

prepare() {
  set -eo pipefail

  # Create git repository to hold gitlab upstream code and create diff
  if [ -d "libxft_upstream" ]; then
    rm -rf "libxft_upstream";
  fi;

  mkdir libxft_upstream
  pushd libxft_upstream
  git init
  git remote add upstream ${LIBXFT_UPSTREAM_URL}
  git fetch --depth=2 upstream ${COMMIT_ID}
  popd

  pushd libXft-${_pkgbasever}
  git --git-dir ../libxft_upstream/.git diff -u ${COMMIT_ID}~ ${COMMIT_ID} | patch -p1
  popd

  set +eo pipefail
}

build() {
  cd libXft-${_pkgbasever}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXft-${_pkgbasever}
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

}
