# Contributor: mar77i <mar77i at mar77i dot ch>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=st-luke-git
_pkgname=st-luke
pkgver=0.8.2.r1148.222eac7
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='https://github.com/LukeSmithxyz/st.git'
arch=('any')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')

# include config.h and any patches you want to have applied here
source=("${_pkgname}::git+$url")
sha256sums=('SKIP')
provides=('st')
conflicts=('st')

pkgver() {
  cd $_pkgname
  echo "$(date '+%Y%m%d%H%M')"
}

prepare() {
  cd "${_pkgname}"
  # skip terminfo which conflicts with nsurses
  sed -i '/tic /d' Makefile
  sed -i 's/mono:pixelsize=14/Fira Code Nerd Font Mono:pixelsize=15/' config.h
  sed -i 's/MODKEY,.*XK_c,.*clipcopy/ControlMask|ShiftMask, XK_C, clipcopy/g' config.h
  sed -i 's/MODKEY,.*XK_v,.*clippaste/ControlMask|ShiftMask, XK_V, clippaste/g' config.h
  sed -i 's/TERMMOD,.*XK_J,/ControlMask, XK_minus,/g' config.h
  sed -i 's/TERMMOD,.*XK_K,/ControlMask, XK_plus,/g' config.h
  sed -i '/.*ControlMask, XK_plus.*/a	{ ControlMask,              XK_equal,           zoom,           {.f = +1} },' config.h
}

build() {
  cd "${_pkgname}"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
